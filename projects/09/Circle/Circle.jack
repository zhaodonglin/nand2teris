// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/09/circle/circle.jack

/** Implements a graphical circle. */
class Circle {

   field int x, y; // screen location of the circle's center
   field int radius; // radius of this circle, in pixels

   /** Constructs a new circle with a given location and radius. */
   constructor Circle new(int Ax, int Ay, int Radius) {
      let x = Ax;
      let y = Ay;
      let radius = Radius;
      do draw();
      return this;
   }

   /** Disposes this circle. */
   method void dispose() {
      do Memory.deAlloc(this);
      return;
   }

   /** Draws the circle on the screen. */
   method void draw() {
      do Screen.setColor(true);
      do Screen.drawCircle(x, y, radius);
      return;
   }

   /** Erases the circle from the screen. */
   method void erase() {
      do Screen.setColor(false);
      do Screen.drawCircle(x, y, radius);
      return;
   }

    /** Increments the circle size by 2 pixels. */
   method void incSize() {
      if (radius > 181){
         return;
      }
      if (((y + radius + 2) < 254) & ((x + radius+2) < 510) & ((x-radius-2)>0) & ((y-radius-2)>0)) {
         do erase();
         let radius = radius + 2;
         do draw();
      }
      return;
   }

   /** Decrements the circle size by 2 pixels. */
   method void decSize() {
      if (radius > 2) {
         do erase();
         let radius = radius - 2;
         do draw();
      }
      return;
   }

   /** Moves the circle up by 2 pixels. */
   method void moveUp() {
      if ((y - 2 - radius) > 0) {
         do Screen.setColor(false);
         do Screen.drawCircle(x, y, radius);
         let y = y - 2;
         do Screen.setColor(true);
         do Screen.drawCircle(x, y, radius);
      }
      return;
   }

   /** Moves the circle down by 2 pixels. */
   method void moveDown() {
      if ((y + 2 + radius) < 254) {
         do Screen.setColor(false);
         do Screen.drawCircle(x, y, radius);
         let y = y + 2;
         do Screen.setColor(true);
         do Screen.drawCircle(x, y, radius);
      }
      return;
   }

   /** Moves the circle left by 2 pixels. */
   method void moveLeft() {
      if ((x - 2 - radius)>0 ) {
         do Screen.setColor(false);
         do Screen.drawCircle(x, y, radius);
         let x = x - 2;
         do Screen.setColor(true);
         do Screen.drawCircle(x, y, radius);
      }
      return;
   }

   /** Moves the circle right by 2 pixels. */
   method void moveRight() {
      if ((x + 2 + radius) < 510) {
         do Screen.setColor(false);
         do Screen.drawCircle(x, y, radius);
         let x = x + 2;
         do Screen.setColor(true);
         do Screen.drawCircle(x, y, radius);
      }
      return;
   }
}
